*$
* Model: PS2801C
*
* This SPICE model is intended to simulate the device behavior at Ta=25C within
* the maximum ratings as called out in the data sheet.
*
* Please note that the model can't duplicate exact device performance in the
* real world under all conditions and they can't replace the breadboarding 
* for final design verification.
*
* Note: CTR is modeled base on sample B in the data sheet. Please refer to the
* data sheet for details.
*********************************************************************************
* A = PIN 1: diode anode
* K = PIN 2: diode cathode
* E = PIN 3: BJT emitter
* C = PIN 4: BJT collector
.SUBCKT PS2801C A K E C
D1 A D LED
Vsense D K 0
Hd   R 0 Vsense 1
Rd   R T 100k
Cd   T 0 20p
Gctr C B TABLE
+ {If(V(T)<5m,
+ ((-3712000000*PWR(V(T),5)+67010000*PWR(V(T),4)
+ -464100*PWR(V(T),3)+1506*PWR(V(T),2)+0.4877*V(T)-0.00005295)*0.96/550),
+ ((680800*PWR(V(T),5)-127100*PWR(V(T),4)+9206*PWR(V(T),3)-320.6*PWR(V(T),2)+
+ 5.518*V(T)-0.008248)*0.95/550))}
+ (0,0) (0.1m, 0.1m)
Q1 C B E detector
.model LED D IS=1p N=1.999644 RS=0 BV=6 IBV=10u
+ CJO=10p EG=1.424 TT=500n
.model detector NPN IS=100p BF=550 NF=1.3 BR=50 TF=2n TR=1.5n
+ CJE=15p CJC=20p VAF=100 ISS=0 CJS=1p
.ends
*$
